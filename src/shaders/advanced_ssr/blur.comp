#version 460 core

#include <gbuffer_encode.glsl>

layout (set = 0, binding = 0) uniform sampler2D DEPTH_TEX;
layout (set = 0, binding = 1) uniform sampler2D NORMAL_TEX;
layout (set = 0, binding = 2) uniform sampler2D REFLECTIONS_TEX;
layout (set = 0, binding = 3) uniform sampler2D MATERIAL_TEX;
layout (set = 0, binding = 4) uniform sampler2D HISTORY_TEX;
layout (set = 0, binding = 5) uniform sampler2D VELOCITY_TEX;
layout (set = 0, binding = 6) uniform sampler2D HISTORY_DEPTH_TEX;
layout (set = 0, binding = 7, rgba8) uniform image2D BLURED_REFLECTION;

const float PI = 3.1415926535897932384626433832795;

layout (push_constant) uniform PushConstants {
  float max_roughness;
  uint accumulate;
  uint disable_blur;
};

layout (set = 0, binding = 8) uniform ReprojectConsts {
  mat4 inverse_camera;
  mat4 prev_inverse_camera;
  vec4 fovy_aspect_znear_zfar;
};

vec3 reconstruct_world_pos(in sampler2D depth_tex, in mat4 inverse_camera, in vec2 screen_uv);

layout (local_size_x = 8, local_size_y = 8) in;
void main() {
  ivec2 tex_size = imageSize(BLURED_REFLECTION);
  ivec2 pixel_pos = ivec2(gl_WorkGroupID.xy * gl_WorkGroupSize.xy + gl_LocalInvocationID.xy);
  vec2 screen_uv = vec2(pixel_pos + vec2(0.5, 0.5))/vec2(tex_size);

  if (pixel_pos.x >= tex_size.x || pixel_pos.y >= tex_size.y) {
    return;
  }
  float roughness = texture(MATERIAL_TEX, screen_uv).g;
  roughness = mix(0.0, max_roughness, roughness);

  float center_depth = texelFetch(DEPTH_TEX, pixel_pos, 1).x; 
  vec3 center_normal = sample_gbuffer_normal(NORMAL_TEX, screen_uv);

  float sigma = mix(0.4, 4, roughness);
  if (disable_blur != 0) {
    sigma = 0.35;
  }

  float weight_sum = 0.f;
  vec3 color = vec3(0, 0, 0);

  int r = int(floor(3 * sigma - 0.01));
  float g = 1.0/(2 * PI * sigma * sigma);
  float e = 2 * sigma * sigma;

  for (int i = -r; i <= r; i++) {
    for (int j = -r; j <= r; j++) {
      ivec2 pos = pixel_pos + ivec2(i, j);
      vec2 uv = vec2(pos)/vec2(tex_size);

      float pixel_depth = texelFetch(DEPTH_TEX, pos, 1).x; 
      vec3 pixel_normal = sample_gbuffer_normal(NORMAL_TEX, uv);

      float bilateral_weight = max(1 - 1000 * abs(center_depth - pixel_depth)/center_depth, 0);
      float normal_weight = max(dot(center_normal, pixel_normal), 0);

      float w = g * exp(-(i*i + j*j)/e);
      w *= bilateral_weight;
      w *= normal_weight;

      color += texelFetch(REFLECTIONS_TEX, pixel_pos + ivec2(i, j), 0).rgb * w;
      weight_sum += w;
    }
  }

  color /= max(weight_sum, 0.001);

  bool reprojected = false;
  vec2 velocity = texture(VELOCITY_TEX, screen_uv).xy;
  float delta_len = length(velocity);
  vec2 prev_uv = screen_uv + velocity;
  
  if (all(greaterThanEqual(prev_uv, vec2(0, 0))) && all(lessThanEqual(prev_uv, vec2(1, 1)))) {
    vec3 v_world_cur = reconstruct_world_pos(DEPTH_TEX, inverse_camera, screen_uv);
    vec3 v_world_prev = reconstruct_world_pos(HISTORY_DEPTH_TEX, prev_inverse_camera, prev_uv);
    vec3 v_camera = vec3(inverse_camera * vec4(0, 0, 0, 1));

    const float MAX_REPROJECTION_EPS = 0.1;
    const float MIN_REPROJECTION_EPS = 0.01;
  
    float error = length(v_world_cur - v_world_prev);
    float pixel_dist = length(v_world_cur - v_camera);
    float velocity_len = length(velocity);
    reprojected = (velocity_len < 0.0001) || (error < clamp(0.1 * pixel_dist * delta_len, MIN_REPROJECTION_EPS, MAX_REPROJECTION_EPS)); 
  }

  if (accumulate == 0) {
    reprojected = false;
  }

  if (reprojected) {
    vec3 history_color = texture(HISTORY_TEX, screen_uv).rgb;
    color = mix(history_color, color, 0.1);
  }

  imageStore(BLURED_REFLECTION, pixel_pos, vec4(color, 0));
}

vec3 reconstruct_world_pos(in sampler2D depth_tex, in mat4 inverse_camera, in vec2 screen_uv) {
  float d = textureLod(depth_tex, screen_uv, 1.0).x;
  vec3 v_camera = reconstruct_view_vec(screen_uv, d, fovy_aspect_znear_zfar.x, fovy_aspect_znear_zfar.y, fovy_aspect_znear_zfar.z, fovy_aspect_znear_zfar.w);
  vec4 v_world = inverse_camera * vec4(v_camera, 1.0);
  return v_world.xyz;
}
