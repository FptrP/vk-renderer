#version 460
#include <screen_trace.glsl>

layout (set = 0, binding = 0) uniform sampler2D DEPTH;
layout (set = 0, binding = 1) uniform sampler2D NORMAL;
layout (set = 0, binding = 2) uniform sampler2D MATERIAL;

layout (set = 0, binding = 3) uniform TraceParams {
  mat4 normal_mat;
  uint frame_random;
  float fovy;
  float aspect;
  float znear;
  float zfar;
};

#define M_PI 3.1415926535897932384626433832795
#define HALTON_SEQ_SIZE 128
#define TILE_SIZE 8

layout (set = 0, binding = 4) uniform HaltonBuffer {
  vec4 halton_vec[HALTON_SEQ_SIZE];
};

layout (push_constant) uniform PushConstants {
  uint reflection_type; //0 - mirror, 1 - glossy
  float max_roughness;
};

vec3 sampleGGXVNDF(vec3 Ve, float alpha_x, float alpha_y, float U1, float U2);
vec3 get_tangent(in vec3 n);
vec3 trace_screen_space_ray(int lod, vec3 ray_origin, vec3 ray_dir, float stride, float jitter, float max_steps, float thikness, out bool valid); 
vec3 uniform_trace(int lod, vec3 ray_origin, vec3 ray_dir, float jitter, float steps_count, float thikness, inout bool valid);

layout (set = 0, binding = 5, rgba16) uniform image2D OUT_RAY;

layout (set = 0, binding = 6) readonly buffer TILES_BUFFER {
  int g_tiles[];
};

float rand(vec2 co);

layout (local_size_x = TILE_SIZE, local_size_y = TILE_SIZE) in;
void main() {
  ivec2 tex_size = imageSize(OUT_RAY);
  int tile_index = g_tiles[gl_WorkGroupID.x];
  const int tile_width = (tex_size.x + TILE_SIZE - 1)/TILE_SIZE;

  ivec2 pixel_pos = ivec2(tile_index % tile_width, tile_index/tile_width);
  pixel_pos = TILE_SIZE * pixel_pos + ivec2(gl_LocalInvocationID.xy);

  vec2 screen_uv = vec2(pixel_pos)/vec2(tex_size);

  if (pixel_pos.x >= tex_size.x || pixel_pos.y >= tex_size.y) {
    return;
  }
  
  /*if (reflection_type == 0) {
    imageStore(OUT_RAY, pixel_pos, vec4(0, 0, 0, 1));
    return;
  }*/

  vec3 material = texture(MATERIAL, screen_uv).rgb;

  float roughness = material.g;
  roughness = mix(0.0, max_roughness, roughness);
  roughness *= roughness;

  float pixel_depth = texture(DEPTH, screen_uv).x;
  vec3 pixel_normal_world = sample_gbuffer_normal(NORMAL, screen_uv);
  vec3 pixel_normal = normalize((normal_mat * vec4(pixel_normal_world, 0)).xyz);
  vec3 view_vec = reconstruct_view_vec(screen_uv, pixel_depth, fovy, aspect, znear, zfar);

  //halton random
  const uint base_index = uint(rand(screen_uv) * HALTON_SEQ_SIZE);
  uint index = (base_index + frame_random) & (HALTON_SEQ_SIZE - 1);
  vec2 rnd = halton_vec[index].xy;
  //sample microphaset normal 
  vec3 tangent = get_tangent(pixel_normal);
  vec3 bitangent = normalize(cross(pixel_normal, tangent));
  tangent = normalize(cross(bitangent, pixel_normal));

  vec3 view_dir = -normalize(view_vec); //e = (tangent, bitangent, normal)
  view_dir = vec3(
    dot(view_dir, tangent),
    dot(view_dir, bitangent),
    dot(view_dir, pixel_normal));

  vec3 brdf_norm = sampleGGXVNDF(view_dir, roughness, roughness, rnd.x, rnd.y);
  vec3 N = brdf_norm.x * tangent + brdf_norm.y * bitangent + brdf_norm.z * pixel_normal;
  vec3 R = reflect(view_vec, N); 
  //
  vec3 ray_start = project_view_vec(view_vec + 0.001 * pixel_normal, fovy, aspect, znear, zfar);
  ray_start.z -= 0.0001;

  vec3 ray_dir = project_view_vec(view_vec + R, fovy, aspect, znear, zfar);
  ray_dir -= ray_start;
  ray_dir *= (1-ray_start.z)/ray_dir.z;

  bool valid_hit = false;
  vec3 out_ray = vec3(0, 0, 0);
  
  if (reflection_type == 0) { //mirror reflections
    out_ray = hierarchical_raymarch(DEPTH, ray_start, ray_dir, 0, 50, valid_hit);
  } else { //glossy
    /*const float ray_step = mix(1.0, 2.0, roughness);
    const float geomery_thikness = mix(0.3, 0.8, roughness);
    
    out_ray = trace_screen_space_ray(2, ray_start, ray_dir, ray_step, 1.0 + 0.5 * rand(screen_uv), 200, geomery_thikness, valid_hit);*/
    out_ray = hierarchical_raymarch(DEPTH, ray_start, ray_dir, 1, 25, valid_hit);
    //out_ray = uniform_trace(1, ray_start, ray_dir, 1 + 0.5 * rand(screen_uv), 20, 1.0, valid_hit);
  }

  vec4 out_ray_info = vec4(0, 0, 1, 1);

  if (valid_hit) {
    vec2 ray_step = abs(out_ray.xy - ray_start.xy) * tex_size;
    if (max(ray_step.x, ray_step.y) < 2.0) {
      valid_hit = false;
    }
  }
  
  if (valid_hit)
  {
    vec3 hit_normal_world = sample_gbuffer_normal(NORMAL, out_ray.xy);
    vec3 hit_normal = (normal_mat * vec4(hit_normal_world, 0)).xyz;

    if (dot(hit_normal, R) > 0 || dot(pixel_normal, R) < 0) {
      valid_hit = false;
    }
  }

  if (valid_hit && reflection_type == 0) {  
    float hit_depth = textureLod(DEPTH, out_ray.xy, 0).x;
    float hit_z = linearize_depth2(hit_depth, znear, zfar);
    float ray_z = linearize_depth2(out_ray.z, znear, zfar);
    if (ray_z > hit_z + 0.3 || ray_z < hit_z - 0.1) {
      valid_hit = false;
    }
  }

  out_ray_info = vec4(out_ray, valid_hit? pixel_depth : 1.0);
  imageStore(OUT_RAY, pixel_pos, out_ray_info);
}

// Input Ve: view direction
// Input alpha_x, alpha_y: roughness parameters
// Input U1, U2: uniform random numbers
// Output Ne: normal sampled with PDF D_Ve(Ne) = G1(Ve) * max(0, dot(Ve, Ne)) * D(Ne) / Ve.z
vec3 sampleGGXVNDF(vec3 Ve, float alpha_x, float alpha_y, float U1, float U2)
{
  // Section 3.2: transforming the view direction to the hemisphere configuration
  vec3 Vh = normalize(vec3(alpha_x * Ve.x, alpha_y * Ve.y, Ve.z));
  // Section 4.1: orthonormal basis (with special case if cross product is zero)
  float lensq = Vh.x * Vh.x + Vh.y * Vh.y;
  vec3 T1 = lensq > 0 ? vec3(-Vh.y, Vh.x, 0) * inversesqrt(lensq) : vec3(1,0,0);
  vec3 T2 = cross(Vh, T1);
  // Section 4.2: parameterization of the projected area
  float r = sqrt(U1);
  float phi = 2.0 * M_PI * U2;
  float t1 = r * cos(phi);
  float t2 = r * sin(phi);
  float s = 0.5 * (1.0 + Vh.z);
  t2 = (1.0 - s)*sqrt(1.0 - t1*t1) + s*t2;
  // Section 4.3: reprojection onto hemisphere
  vec3 Nh = t1*T1 + t2*T2 + sqrt(max(0.0, 1.0 - t1*t1 - t2*t2))*Vh;
  // Section 3.4: transforming the normal back to the ellipsoid configuration
  vec3 Ne = normalize(vec3(alpha_x * Nh.x, alpha_y * Nh.y, max(0.0, Nh.z)));
  return Ne;
}

vec3 get_tangent(in vec3 n) {
  float max_xy = max(abs(n.x), abs(n.y));
  vec3 t;
  
  if (max_xy < 0.00001) {
    t = vec3(1, 0, 0);
  } else {
    t = vec3(n.y, -n.x, 0);
  }

  return normalize(t);
}

float rand(vec2 co){
  return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

float distance_squared(vec3 a, vec3 b) {
  vec3 c = b - a;
  return dot(c, c);
}

vec3 trace_screen_space_ray(int lod, vec3 ray_origin, vec3 ray_dir, float stride, float jitter, float max_steps, float thikness, out bool valid) {
  vec3 tex_size = vec3(vec2(textureSize(DEPTH, lod)), 1.0);
  
  vec3 P0 = tex_size * ray_origin;
  vec3 P1 = tex_size * clip_screen(ray_origin, ray_origin + ray_dir);
  P1.xy += vec2((distance_squared(P0, P1) < 0.0001) ? 0.01 : 0.0);

  vec3 delta = P1 - P0;
  bool permute = false;
  
  if (abs(delta.x) < abs(delta.y)) {
    delta = delta.yxz;
    P0 = P0.yxz;
    P1 = P1.yxz;
    permute = true;
  }

  float step_dir = sign(delta.x);
  float invdx = step_dir/delta.x;
  
  vec3 dP = vec3(step_dir, delta.y * invdx, delta.z * invdx);
  P0 += dP * jitter;
  dP *= stride;

  float end_x = P1.x * step_dir;
  float steps_count = 0.0;
  vec3 out_ray;
  valid = false;

  for (vec3 P = P0; (P.x * step_dir <= end_x) && (steps_count < max_steps); P += dP, steps_count += 1.0) {
    vec3 hit_pixel = permute? P.yxz : P.xyz;
    float sampled_depth = texelFetch(DEPTH, ivec2(hit_pixel.xy), lod).x;
    
    float ray_z = linearize_depth2(hit_pixel.z, znear, zfar);
    float scene_z = linearize_depth2(sampled_depth, znear, zfar);
    
    if (ray_z <= (scene_z - 1e-4) && ray_z >= scene_z - thikness) {
      valid = true;
      out_ray = hit_pixel/tex_size;    
      break;
    }

  }

  return out_ray;
}

vec3 initial_ray_step(vec3 P0, vec3 P1) {
  vec3 delta = P1 - P0;
  bool permute = false;

  if (abs(delta.x) < abs(delta.y)) {
    permute = true;
    delta = delta.yxz;
  }

  float step_dir = sign(delta.x);
  float invdx = step_dir/delta.x;
  
  vec3 dP = vec3(step_dir, delta.y * invdx, delta.z * invdx);
  return permute? dP.yxz : dP.xyz;
}

vec3 uniform_trace(int lod, vec3 ray_origin, vec3 ray_dir, float jitter, float steps_count, float thikness, inout bool valid) {
  vec3 tex_size = vec3(vec2(textureSize(DEPTH, lod)), 1.0);
  vec3 P0 = tex_size * ray_origin;
  vec3 P1 = tex_size * clip_screen(ray_origin, ray_origin + ray_dir);

  P0 += initial_ray_step(P0, P1) * jitter;

  vec3 dP = (P1 - P0)/steps_count;
  vec3 P = P0;
  
  valid = false;
  vec3 out_ray = vec3(0);

  while (steps_count > 0) {
    float sampled_depth = texelFetch(DEPTH, ivec2(P.xy), lod).x;
    float ray_z = linearize_depth2(P.z, znear, zfar);
    float scene_z = linearize_depth2(sampled_depth, znear, zfar);
    
    if (ray_z <= (scene_z - 1e-4) && ray_z >= scene_z - thikness) {
      valid = true;
      out_ray = P/tex_size;    
      break;
    }
    
    P += dP;
    steps_count -= 1.0;
  }

  return out_ray;
}